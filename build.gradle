buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'gradle.plugin.org.jetbrains:gradle-intellij-plugin:0.0.35'
    }
}

plugins {
    id 'net.researchgate.release' version '2.3.4'
    id "net.saliman.cobertura" version "2.3.0"
    id "com.github.kt3k.coveralls" version "2.6.3"
}

allprojects {
    repositories {
        mavenCentral()
    }

    allprojects {
        apply plugin: 'cobertura'
        apply plugin: 'com.github.kt3k.coveralls'
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    group = 'com.google.gct'

    task jarSources(type:Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
    }
    assemble.dependsOn jarSources

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        version = ideaVersion
        downloadSources = true
        sandboxDirectory = "${rootProject.buildDir}/idea-sandbox"

        publish {
            username System.getenv('IJ_REPO_USERNAME')
            password System.getenv('IJ_REPO_PASSWORD')
            channel ijPluginRepoChannel
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            def provided = [["org.apache.httpcomponents", "httpclient"],
                            ["com.google.guava", "guava"]]
            eachDependency { DependencyResolveDetails dependency ->
                if (provided.contains([dependency.requested.group, dependency.requested.name])) {
                    throw new GradleException('Dependency "' + dependency.requested.name +
                            '" should not be included in your build')
                }
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile ('org.mockito:mockito-core:1.9.5') {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    apply plugin: 'findbugs'
    findbugs {
        ignoreFailures = true
        sourceSets = [sourceSets.main]
        effort = "max"
        toolVersion = '2.0.3'
    }

    apply plugin: 'net.saliman.cobertura'
    configurations.all {
        resolutionStrategy {
            force 'junit:junit:4.11'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    apply plugin: 'pmd'
    pmd {
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    apply plugin: 'jdepend'
    configurations.all {
        resolutionStrategy {
            // JDepend manages to include a different version of itself in its own transitive closure.
            force "jdepend:jdepend:${project.jdepend.toolVersion}"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'net.saliman.cobertura'
//cobertura.coverageFormats = ['html', 'xml']
//cobertura {
////    rootProject.subprojects.each {
////        coverageDirs << file("${it.name}/build/classes/main")
////    }
//    coverageMergeDatafiles = [
//            new File("${rootProject.rootDir}/google-account-plugin/build/cobertura/cobertura.ser"),
//            new File("${rootProject.rootDir}/google-cloud-tools-plugin/build/cobertura/cobertura.ser")
//    ]
//}

tasks.coveralls {
    dependsOn 'check', 'cobertura'
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageReportDatafile = file("build/cobertura/cobertura.ser")
    coverageReportDir = file("build/reports/coverage")

    coverageDirs = []
    coverageSourceDirs = []
    coverageMergeDatafiles = []

    rootProject.subprojects.each {
        coverageFormats = ['html', 'xml']
        coverageDirs << file("${it.buildDir}/classes/main")
        coverageSourceDirs << file("${it.projectDir}/src/main/java")
        coverageMergeDatafiles << file("${it.buildDir}/cobertura/cobertura.ser")
    }
}

coveralls {
    coberturaReportPath = "${rootProject.buildDir}/reports/coverage/coverage.xml"
}

// Forces these submodules to have tests executed first so that we can properly
// merge Cobertura test coverage reports
test.dependsOn([":common-lib:test", ":common-test-lib:test", ":google-account-plugin:test", ":google-cloud-tools-plugin:test"])